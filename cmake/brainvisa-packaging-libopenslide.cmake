find_package( OpenSlide ) 

function( BRAINVISA_PACKAGING_COMPONENT_INFO component package_name package_maintainer package_version )
  # Find version
  set( version )
  if(OPENSLIDE_VERSION)
    set(version ${OPENSLIDE_VERSION})
  else()
    if( OPENSLIDE_LIBRARIES )
        foreach( lib ${OPENSLIDE_LIBRARIES} )
        get_filename_component( real "${lib}" REALPATH )
        string( REGEX MATCH "^.*libopenslide${CMAKE_SHARED_LIBRARY_SUFFIX}[.](.*)$" match "${real}" )
        if( match )
            set( version "${CMAKE_MATCH_1}" )
            break()
        endif()
        endforeach()
    endif()
  endif()
  
  if( version )
    set( ${package_version} "${version}" PARENT_SCOPE )
  else()
    set( ${package_version} "0.0.0" PARENT_SCOPE )
  endif()
  set( ${package_name} ${component} PARENT_SCOPE )
  set( ${package_maintainer} "IFR 49" PARENT_SCOPE )
  
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libcairo2 RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libgdk-pixbuf2 RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libpng12 RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libjpeg62 RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libtiff RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libopenjpeg RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libjpegxr RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS glib RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS gobject RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS gio RUN )
  BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libsqlite3-0 RUN )
  if(WIN32)
    BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libfontconfig1 RUN )
  else()
    # ship libfontconfig only on Mandriva 2008 packages
    execute_process( COMMAND "${CMAKE_BINARY_DIR}/bin/bv_system_info" RESULT_VARIABLE result OUTPUT_VARIABLE output OUTPUT_STRIP_TRAILING_WHITESPACE )
    if( result EQUAL 0 )
        string( REGEX MATCH "Mandriva-2008.*" mdv ${output} )
        if( mdv )
        BRAINVISA_THIRDPARTY_DEPENDENCY( "${component}" RUN DEPENDS libfontconfig1 RUN )
        endif()
    endif()
  endif()
  
endfunction()

function( BRAINVISA_PACKAGING_COMPONENT_RUN component )
  if( OPENSLIDE_FOUND)
    BRAINVISA_INSTALL_RUNTIME_LIBRARIES( ${component} ${OPENSLIDE_LIBRARIES} )
    set(${component}_PACKAGED TRUE PARENT_SCOPE)
  else()
    set(${component}_PACKAGED FALSE PARENT_SCOPE)
  endif()
endfunction()

# this variable declares the install rule for the dev package
set( libopenslide-dev-installrule TRUE )

function( BRAINVISA_PACKAGING_COMPONENT_DEV component )
  if(OPENSLIDE_FOUND)
    foreach( _dir ${OPENSLIDE_INCLUDE_DIRS} )
      get_filename_component( _name ${_dir} NAME )
      if( "${_name}" STREQUAL "openslide" )
        BRAINVISA_INSTALL_DIRECTORY( "${_dir}" include/openslide
          ${component}-dev )
      endif()
    endforeach()
    set(${component}-dev_PACKAGED TRUE PARENT_SCOPE)
  else()
    set(${component}-dev_PACKAGED FALSE PARENT_SCOPE)
  endif()
endfunction()
